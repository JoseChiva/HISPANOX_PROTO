#<AdxTL>@(#)0.0.0.0 $Revision$ Vales de preparación
#
#MLGA - Al cambiar el Vale de Preparacion a ENTREGABLE(V) se general el XML SOR - 07/2020
# 06.302.178 - JC.18012022.camp entrada texto línea VP -  LneComment en el SOR
# 06.308.862 - JC.02022022.VP diferent adreça entrega
# 06.317.883 - JC.06102022.CONSULTA  campo Total Crédito
# 06.414.395 - JC.01122022.Saldo contable con negativos
#############################################################################################

$ACTION
#If GUSER="ADEV" Then : Infbox ACTION : Endif
Case ACTION
  When "APRES_MOD"  : Gosub APRES_MOD
  When "LIENS"      : Gosub LIENS
  When "EXEBOUT"    : Gosub EXEBOUT
  When "SETBOUT"    : Gosub SETBOUT
  When "SEL_TABLE"  : Gosub SEL_TABLE
  When "VERF_TABLE" : Gosub VERF_TABLE
  When "AFFMASK"    : Gosub AFFMASK
  When "ANNULE"     : Gosub ANNULE
  When Default
Endcase
Return

##############################################################
######################    ACCIONES    ########################
##############################################################
##############################################################
$APRES_MOD
Local Integer LCON

# 06.308.862.ini
  If !clalev([F:ZSOH0]) Then : Local File SORDER [F:ZSOH0] : Endif
# 06.308.862.fin

# JC.03062021.STR
  If !clalev([F:PRE0]) Then : Local File STOPRED [F:PRE0] : Endif
  For LCON=0 To [M:PRH1]NBLIG-1
    Read [F:PRE0]PRE0 = [M:PRH0]PRHNUM;[M:PRH1]PRELIN(LCON)
    If !fstat Then
      Trbegin [F:PRE0]
      [F:PRE0]ZEXPORT     = [M:PRH1]ZEXPORT(LCON)
      [F:PRE0]ZTXTALMACEN = [M:PRH1]ZTXTALMACEN(LCON)             # 06.302.178.new
# 06.308.862.ini
      Read [F:ZSOH0]SOH0  = [M:PRH1]ORINUM(LCON)
      If !fstat Then
        [F:PRE0]ZBPAADD     = [F:ZSOH0]BPAADD
        [F:PRE0]ZBPDNAM0    = [F:ZSOH0]BPDNAM(0)
        [F:PRE0]ZBPDNAM1    = [F:ZSOH0]BPDNAM(1)
        [F:PRE0]ZBPDADDLIG0 = [F:ZSOH0]BPDADDLIG(0)
        [F:PRE0]ZBPDADDLIG1 = [F:ZSOH0]BPDADDLIG(1)
        [F:PRE0]ZBPDADDLIG2 = [F:ZSOH0]BPDADDLIG(2)
        [F:PRE0]ZBPDPOSCOD  = [F:ZSOH0]BPDPOSCOD
        [F:PRE0]ZBPDCTY     = [F:ZSOH0]BPDCTY
        [F:PRE0]ZBPDSAT     = [F:ZSOH0]BPDSAT
        [F:PRE0]ZBPDCRY     = [F:ZSOH0]BPDCRY
        [F:PRE0]ZBPDCRYNAM  = [F:ZSOH0]BPDCRYNAM
      Endif
# 06.308.862.fin
      Rewrite [F:PRE0]
      If !fstat Then
        Commit
      Else
        Rollback
      Endif
    Endif
  Next
  Close Local File [PRE0]
  If dim(LCON)>0 Then : Kill LCON :Endif
#  Call ZSGASOR([M:PRH0]PRHNUM,'') From ZSGASOR
# JC.03062021.END

Return

##############################################################
$LIENS
  For X=0 To [M:PRH1]NBLIG-1
    [M:PRH1]ZUSRFLD1(X)=[F:PRE]ZUSRFLD1
  Next
Return

##############################################################
$EXEBOUT
  Case BOUT
    When "s"  : Gosub CREA_SOR                      # JC.20052021
  Endcase
Return

##############################################################
$SETBOUT
#  If [M:PRH0]ZSGAENVIADO = 2 Then
#    Call VIREBOUT(CHAINE,"s") From GOBJET
#  Else
    CHMEN += "s"
#  Endif
Return

##############################################################
$SEL_TABLE
  Case TABLE
    When "ZSELVPADRE"   : Gosub SEL_ZSELVPADRE
  Endcase
Return

##############################################################
$VERF_TABLE
  Case TABLE
    When "ZSELVPADRE"   : Gosub VERF_ZSELVPADRE
  Endcase
Return

##############################################################
$AFFMASK
# 06.317.883.ini
  If val(func AFNC.PARAM("ZVPCONCRED","")) = 2 Then
    If dim(GGENPRH) > 0 Then
      If GGENPRH = 1 Then
        Local Decimal LORDATI

        Gosub AFFMASK      From SUBPRHA

        # actualiza el encurso
        # desasigna el encurso de los pedidos
        If !clalev([F:ZPR0]) Then : Local File STOPRED [F:ZPR0] : Endif
        If !clalev([F:ZSOH]) Then : Local File SORDER  [F:ZSOH] : Endif
        If !clalev([F:ZSOP]) Then : Local File SORDERP [F:ZSOP] : Endif
        If !clalev([F:ZSOQ]) Then : Local File SORDERQ [F:ZSOQ] : Endif
        Filter [F:ZPR0] Where PRHNUM = [F:PRH]PRHNUM
        For [ZPR0]
          Read [F:ZSOH]SOH0 = [F:ZPR0]ORINUM
          Read [F:ZSOP]SOP0 = [F:ZPR0]ORINUM;[F:ZPR0]ORILIN;[F:ZPR0]ORISEQ
          Read [F:ZSOQ]SOQ0 = [F:ZPR0]ORINUM;[F:ZPR0]ORILIN;[F:ZPR0]ORISEQ
          If !fstat Then
# 06.414.395.ini
#            Call  MAJMVC ([F:ZSOH]BPCORD,[F:ZSOH]CUR,[F:ZSOH]CHGTYP,0,[F:ZSOH]ORDDAT,[F:ZSOH]SOHNUM,[F:ZSOP]SALFCY,
#&                         [F:ZSOQ]QTY*[F:ZSOP]NETPRINOT,[F:ZSOQ]QTY*[F:ZSOP]NETPRIATI,'-','F',"",[F:ZSOP]SOHCAT) From TRTBPMVT
            Call  MAJMVC ([F:ZSOH]BPCORD,[F:ZSOH]CUR,[F:ZSOH]CHGTYP,0,[F:ZSOH]ORDDAT,[F:ZSOH]SOHNUM,[F:ZSOP]SALFCY,
&                         ar2([F:ZSOQ]QTY*[F:ZSOP]NETPRINOT),ar2([F:ZSOQ]QTY*[F:ZSOP]NETPRIATI),'-','F',"",[F:ZSOP]SOHCAT) From TRTBPMVT
# 06.414.395.fin
            LORDATI       += [F:ZSOQ]QTY*[F:ZSOP]NETPRIATI
          Endif
        Next
        Filter [F:ZPR0]
        # asigna el encurso al vale de preparación
        If LORDATI > 0 Then
# 06.414.395.ini
#          Call  MAJMVC ([F:PRH]BPCORD,[F:ZSOH]CUR,[F:ZSOH]CHGTYP,0,[F:PRH]SHIDAT,[F:PRH]PRHNUM,[F:PRH]STOFCY,
#&                       0.0,LORDATI,'+','O',"",[F:ZSOH]SOHCAT) From TRTBPMVT
          Call  MAJMVC ([F:PRH]BPCORD,[F:ZSOH]CUR,[F:ZSOH]CHGTYP,0,[F:PRH]SHIDAT,[F:PRH]PRHNUM,[F:PRH]STOFCY,
&                       0.0,ar2(LORDATI),'+','O',"",[F:ZSOH]SOHCAT) From TRTBPMVT
# 06.414.395.fin
        Endif
        GPE=1
      Endif
    Endif
  Endif
# 06.317.883.fin
Return

##############################################################
$ANNULE
# 06.317.883.ini
Local Integer LCON
Local Decimal LORDATI

  If val(func AFNC.PARAM("ZVPCONCRED","")) = 2 Then
    Gosub ANNULE       From SUBPRHA

    # actualiza el encurso
    # asigna el encurso a los pedidos
    If !clalev([F:ZSOH]) Then : Local File SORDER  [F:ZSOH] : Endif
    If !clalev([F:ZSOP]) Then : Local File SORDERP [F:ZSOP] : Endif
    If !clalev([F:ZSOQ]) Then : Local File SORDERQ [F:ZSOQ] : Endif
    For LCON=0 To [M:PRH1]NBLIG-1
      Read [F:ZSOH]SOH0 = [M:PRH1]ORINUM(LCON)
      Read [F:ZSOP]SOP0 = [M:PRH1]ORINUM(LCON);[M:PRH1]ORILIN(LCON);[M:PRH1]ORISEQ(LCON)
      Read [F:ZSOQ]SOQ0 = [M:PRH1]ORINUM(LCON);[M:PRH1]ORILIN(LCON);[M:PRH1]ORISEQ(LCON)
      If !fstat Then
# 06.414.395.ini
#        Call  MAJMVC ([F:ZSOH]BPCORD,[F:ZSOH]CUR,[F:ZSOH]CHGTYP,0,[F:ZSOH]ORDDAT,[F:ZSOH]SOHNUM,[F:ZSOP]SALFCY,
#&                     [F:ZSOQ]QTY*[F:ZSOP]NETPRINOT,ar2([F:ZSOQ]QTY*[F:ZSOP]NETPRIATI),'+','F',"",[F:ZSOP]SOHCAT) From TRTBPMVT
        Call  MAJMVC ([F:ZSOH]BPCORD,[F:ZSOH]CUR,[F:ZSOH]CHGTYP,0,[F:ZSOH]ORDDAT,[F:ZSOH]SOHNUM,[F:ZSOP]SALFCY,
&                     ar2([F:ZSOQ]QTY*[F:ZSOP]NETPRINOT),ar2([F:ZSOQ]QTY*[F:ZSOP]NETPRIATI),'+','F',"",[F:ZSOP]SOHCAT) From TRTBPMVT
# 06.414.395.fin
        LORDATI       += [F:ZSOQ]QTY*[F:ZSOP]NETPRIATI
      Endif
    Next
    # desasigna el encurso del vale de preparación
    If LORDATI > 0 Then
# 06.414.395.ini
#      Call  MAJMVC ([F:PRH]BPCORD,[F:ZSOH]CUR,[F:ZSOH]CHGTYP,0,[F:PRH]SHIDAT,[F:PRH]PRHNUM,[F:PRH]STOFCY,
#&                   0.0,LORDATI,'-','O',"",[F:ZSOH]SOHCAT) From TRTBPMVT
      Call  MAJMVC ([F:PRH]BPCORD,[F:ZSOH]CUR,[F:ZSOH]CHGTYP,0,[F:PRH]SHIDAT,[F:PRH]PRHNUM,[F:PRH]STOFCY,
&                   0.0,ar2(LORDATI),'-','O',"",[F:ZSOH]SOHCAT) From TRTBPMVT
# 06.414.395.fin
    Endif
    GPE=1
  Endif
# 06.317.883.fin
Return

##############################################################
######################    ETIQUETAS    #######################
##############################################################
##############################################################
$SEL_ZSELVPADRE
Local Clbfile LCRIT(2)

  If !clalev([F:ZVVP1]) Then : Local File ZVVPPADRE1 [F:ZVVP1] : Endif
  LCRIT  = "BPCORD = [M:PRH0]BPCORD"                   # del mismo cliente
  LCRIT += " and DLVFLG = 1"                           # sólo estado = encurso
  LCRIT += " and PRHNUM <> [M:PRH0]PRHNUM"             # no muestra el propio VP
  # sólo VPs con direcciones de entrega iguales en las líneas de los pedidos
# 06.308.862.ini
  LCRIT += " and BPAADDSOH   = [M:PRH1]ZBPAADD(0)"
  LCRIT += " and BPDNAM0     = [M:PRH1]ZBPDNAM0(0)"
  LCRIT += " and BPDNAM1     = [M:PRH1]ZBPDNAM1(0)"
  LCRIT += " and BPDADDLIG0  = [M:PRH1]ZBPDADDLIG0(0)"
  LCRIT += " and BPDADDLIG1  = [M:PRH1]ZBPDADDLIG1(0)"
  LCRIT += " and BPDADDLIG2  = [M:PRH1]ZBPDADDLIG2(0)"
  LCRIT += " and BPDPOSCOD   = [M:PRH1]ZBPDPOSCOD(0)"
  LCRIT += " and BPDCTY      = [M:PRH1]ZBPDCTY(0)"
  LCRIT += " and BPDSAT      = [M:PRH1]ZBPDSAT(0)"
  LCRIT += " and BPDCRY      = [M:PRH1]ZBPDCRY(0)"
  LCRIT += " and BPDCRYNAM   = [M:PRH1]ZBPDCRYNAM(0)"
# 06.308.862.ini
  Default File [F:ZVVP1]
  Filter [F:ZVVP1] Where evalue(LCRIT)

  NBCOL       = 0                                                               # número de la columna
  TIT(NBCOL)  = "Selección Vales de preparación"                                # título de la ventana
  NBCOL       += 1
  TIT(NBCOL)  = "Vales de preparación"                                          # título de la primera columna
  COL(NBCOL)  = "PRHNUM"                                                        # nombre del campo a mostrar en la segunda columna
  NBCOL       += 1
  TIT(NBCOL)  = "Cliente pdo"                                                   # título de la segunda columna
  COL(NBCOL)  = "BPCORD"                                                        # nombre del campo a mostrar en la primera columna
  NBCOL       += 1
  TIT(NBCOL)  = "Dirección"                                                     # título de la tercera columna
  COL(NBCOL)  = "BPAADD"                                                        # nombre del campo a mostrar en la primera columna
  NBCOL       += 1
  TIT(NBCOL)  = "Fecha exp."                                                    # título de la cuarta columna
  COL(NBCOL)  = "SHIDAT"                                                        # nombre del campo a mostrar en la primera columna
  NBCOL       += 1
  TIT(NBCOL)  = "Estado"                                                        # título de la quinta columna
  COL(NBCOL)  = "ESTADO"                                                        # nombre del campo a mostrar en la primera columna
  NBCOL       += 1
  TIT(NBCOL)  = "Tipo entrega"                                                  # título de la sexta columna
  COL(NBCOL)  = "SDHTYP"                                                        # nombre del campo a mostrar en la primera columna
  NBCOL       += 1
  TIT(NBCOL)  = "Status Mecalux"                                                # título de la septima columna
  COL(NBCOL)  = "ZSTATUS"                                                       # nombre del campo a mostrar en la primera columna
Return

##############################################################
$VERF_ZSELVPADRE
Return

##############################################################
# JC.20052021
$CREA_SOR
Local Integer YESNO : YESNO = 2   # yes
Local Integer LCON

  Call OUINON("Se va a genera el documento SOR para el vale de preparación " + [M:PRH0]PRHNUM + "\¿Deseas continuar?",YESNO) From GESECRAN
  If YESNO = 2 Then
    Call ZSGASOR([M:PRH0]PRHNUM,'') From ZSGASOR
    # actualiza cabecera vales de preparación
    If !clalev([F:ZPRH]) Then : Local File STOPREH [F:ZPRH] : Endif
    Read [F:ZPRH]PRH0 = [M:PRH0]PRHNUM
    If !fstat Then
      Trbegin [F:ZPRH]
      [F:ZPRH]ZSGAENVIADO = 2
      Rewrite [F:ZPRH]
      If !fstat Then
        Commit
        [M:PRH0]ZSGAENVIADO = 2
        Affzo [M:PRH0]ZSGAENVIADO
      Else
        Rollback
      Endif
    Endif
    Close Local File [ZPRH]
    # actualiza líneas vales de preparación
    If !clalev([F:ZPRE]) Then : Local File STOPRED [F:ZPRE] : Endif
    For LCON=0 To [M:PRH1]NBLIG-1
      Read [F:ZPRE]PRE0 = [M:PRH0]PRHNUM;[M:PRH1]PRELIN(LCON)
      If !fstat Then
        Trbegin [F:ZPRE]
        [F:ZPRE]ZEXPORT       = 2
        Rewrite [F:ZPRE]
        If !fstat Then
          Commit
          [M:PRH1]ZEXPORT(LCON) = 2
          Affzo [M:PRH1]ZEXPORT(LCON)
        Else
          Rollback
        Endif
      Endif
    Next
    Close Local File [ZPRE]
  Endif

  If dim(LCON)>0 Then : Kill LCON : Endif
Return


##############################################################
#################    ACCIONES DE CAMPO    ####################
##############################################################
######################################################################################
## Etiqueta añadida por el supervisor (pantalla PRH1) 30/09/2020 08:45:46 (MLGA)
######################################################################################
Subprog AM_NBLIG
  If [M:PRH1]ZEXPORT(nolign-1)=0
    [M:PRH1]ZEXPORT(nolign-1)=1
  Endif
  If [M:PRH1]ZEXPORT(nolign-1)=2
    [M:PRH1]ZEXPORT(nolign-1)=3
  Endif
  Affzo [M:PRH1]ZEXPORT(nolign-1)
End


#######################################################################################
### Etiqueta añadida por el supervisor (pantalla PRH0) 30/09/2020 18:46:37 (MLGA)
#######################################################################################
#Subprog S_ZVPPADRE(VALEUR)
#Variable Char    VALEUR()
#
#Local Char LCRIT(250)
#
#  LCRIT  = "[F:PRH]BPCORD = [M:PRH0]BPCORD"                   # del mismo cliente
##  LCRIT += " and [F:PRH]DLVFLG <> 2 and [F:PRH]DLVFLG <> 3"   # no entregables ni entregados
#  LCRIT += " and [F:PRH]DLVFLG = 1"                           # sólo estado = encurso
#  LCRIT += " and [F:PRH]PRHNUM <> [M:PRH0]PRHNUM"             # no muestra el propio VP
#  Filter [F:PRH] Where evalue(LCRIT)
#  Call SELECT("PRH","",[M:PRH0]ZVPPADRE,"") From SELOBJ
#  Filter [F:PRH]
#  If status = 7 : VALEUR='': End
#  Else
#      VALEUR = [F:PRH]PRHNUM
#  Endif
#End

######################################################################################
## Después de modificar el campo 'Enviado SGA' (pantalla PRH0) 03/06/2021 09:18:30 (ADEV)
######################################################################################
# JC.20052021
Subprog AM_ZSGAENVIADO(VALEUR)
Variable Integer VALEUR

Local Integer LCON

  For LCON=0 To [M:PRH1]NBLIG-1
    [M:PRH1]ZEXPORT(LCON) = VALEUR
    Affzo [M:PRH1]ZEXPORT(LCON)
  Next

  If dim(LCON) > 0  Then : Kill LCON : Endif
End

######################################################################################
## Control en el campo 'VP Padre' (pantalla PRH0) 29/09/2021 09:47:10 (ADEV)
######################################################################################
Subprog C_ZVPPADRE(VALEUR)
Variable Char    VALEUR()
# JC.29092021.INI
# impide que se ponga cualquier valor en el campo, sólo admite VP válidos.
# VP válidos significa no entregables ni entregados, del mismo cliente entrega y
# teniendo en cuenta que los % de descuento comercial y %dto pp (elementos de facturación)
# sean iguales, porque si son distintos no pueden ser entregados conjuntamente en una
# misma entrega
Local Integer LCON,LSTA
Local Char    LCRIT(250)
  If VALEUR <> "" Then
    # impide que pongan el mismo VP
    If VALEUR = [M:PRH0]PRHNUM Then
      LSTA = 1
      Goto CREA_BLOQUEO
    Else
      # impide que pongan un VP que no existe
      Read [F:PRH]PRH0 = VALEUR
      If fstat <> 0 Then
        LSTA = 2
        Goto CREA_BLOQUEO
      Else
        # impide que ponga un VP con estado distinto a Encurso
        If [F:PRH]DLVFLG <> 1 Then
          LSTA = 3
          Goto CREA_BLOQUEO
        Else
          #impide que ponga un VP de otro cliente
          If [F:PRH]BPCORD <> [M:PRH0]BPCORD Then
            LSTA = 4
            Goto CREA_BLOQUEO
          Else
            # impide que pongan un VP con diferentes % en los elementos de facturación
            # de sus pedidos origen
            If !clalev([F:PRH0]) Then : Local File STOPREH [F:PRH0] : Endif
            If !clalev([F:PRE0]) Then : Local File STOPRED [F:PRE0] : Endif
            If !clalev([F:SOH0]) Then : Local File SORDER  [F:SOH0] : Endif
            If !clalev([F:SOH1]) Then : Local File SORDER  [F:SOH1] : Endif
            # filtra por el VP Padre seleccionado
            Read [F:PRH0]PRH0 = VALEUR
            If !fstat Then
              # filtra las líneas del VP Padre seleccionado
              Filter [F:PRE0] Where PRHNUM = [F:PRH0]PRHNUM
              For [F:PRE0]
                For LCON=0 To [M:PRH1]NBLIG-1
                  # busca los pedidos origen del VP Padre y del VP activo para comparar los %
                  Read [F:SOH0]SOH0 = [F:PRE0]ORINUM
                  Read [F:SOH1]SOH0 = [M:PRH1]ORINUM(LCON)
                  # los % de dto.comercial y dto.pp no coinciden
                  If [F:SOH0]INVDTAAMT(0) <> [F:SOH1]INVDTAAMT(0) or [F:SOH0]INVDTAAMT(2) <> [F:SOH1]INVDTAAMT(2) Then
                    LSTA = 5
                    Goto CREA_BLOQUEO
                  Endif
                Next  # [M:PRH1]
              Next  # [F:PRE0]
              Filter [F:PRE0]
            Endif
            Close Local File [PRH0],[PRE0],[SOH0],[SOH1]
# 06.308.862.ini
            If !clalev([F:ZVVP2]) Then : Local File ZVVPPADRE1 [F:ZVVP2] : Endif
            Filter [F:ZVVP2] Where BPAADDSOH  = [M:PRH1]ZBPAADD(0)      and BPDNAM0     = [M:PRH1]ZBPDNAM0(0)     and BPDNAM1     = [M:PRH1]ZBPDNAM1(0)     and
&                                  BPDADDLIG0 = [M:PRH1]ZBPDADDLIG0(0)  and BPDADDLIG1  = [M:PRH1]ZBPDADDLIG1(0)  and BPDADDLIG2  = [M:PRH1]ZBPDADDLIG2(0)  and
&                                  BPDPOSCOD  = [M:PRH1]ZBPDPOSCOD(0)   and BPDCTY      = [M:PRH1]ZBPDCTY(0)      and BPDSAT      = [M:PRH1]ZBPDSAT(0)      and
&                                  BPDCRY     = [M:PRH1]ZBPDCRY(0)      and BPDCRYNAM   = [M:PRH1]ZBPDCRYNAM(0)   and PRHNUM      = VALEUR
            Read [F:ZVVP2] First
            If fstat <> 0 Then
              LSTA = 6
              Goto CREA_BLOQUEO
            Endif
            Filter [F:ZVVP2]
            Close Local File [ZVVP2]
# 06.308.862.fin
          Endif
        Endif
      Endif
    Endif
  Endif

  $CREA_BLOQUEO

  Case LSTA
    When 1
      Call ERREUR("El VP padre no puede se el mismo vale") From GESECRAN
      Raz [M:PRH0]ZVPPADRE
      Affzo [M:PRH0]ZVPPADRE
      mkstat = 2
    When 2
      Call ERREUR("El VP padre indicado no existe") From GESECRAN
      Raz [M:PRH0]ZVPPADRE
      Affzo [M:PRH0]ZVPPADRE
      mkstat = 2
    When 3
      Call ERREUR("El VP padre indicado no tiene estado Encurso") From GESECRAN
      Raz [M:PRH0]ZVPPADRE
      Affzo [M:PRH0]ZVPPADRE
      mkstat = 2
    When 4
      Call ERREUR("El VP padre indicado no es del mismo cliente") From GESECRAN
      Raz [M:PRH0]ZVPPADRE
      Affzo [M:PRH0]ZVPPADRE
      mkstat = 2
    When 5
      Call ERREUR("No se puede seleccionar este VP porque los pedidos tienen distintos elementos de facturación (dto o dto pp)") From GESECRAN
      Raz [M:PRH0]ZVPPADRE
      Affzo [M:PRH0]ZVPPADRE
      mkstat = 2
    When 6
      Call ERREUR("No se puede seleccionar este VP porque las direcciones de los pedidos son distintas") From GESECRAN
      Raz [M:PRH0]ZVPPADRE
      Affzo [M:PRH0]ZVPPADRE
      mkstat = 2
    When Default
  Endcase
# JC.29092021.END
End

##############################################################
##################    FUNCIONES PROPIAS    ###################
##############################################################
