#<AdxTL>@(#)0.0.0.0 $Revision$ Recepciones
# 06.300.491 - JC.01022022.Intrastat. Desarrollo automatización país origen
# 06.314.025 - JC.07032022.Adapación generación ROR desde Recepciones para subcontratación
################################################################################

$ACTION

#If GUSER="ADEV" Then : Infbox ACTION : Endif
Case ACTION
  When "APRES_CRE": Gosub APRES_CRE
  When "APRES_MOD": Gosub APRES_MOD
  When "AP_ANNULE": Gosub AP_ANNULE
  When "SETBOUT": Gosub SETBOUT
  When "LIENS": Gosub LIENS
  When "PICKE" : Gosub PICKE
  When Default
Endcase
Return

##############################################################
######################    ACCIONES    ########################
##############################################################
$LIENS
Titcol [M:PTH1]NBLIG "Arancel" For CPRCOE
Return

##############################################################
$PICKE
Gosub ACTION From SUBPTH
GPE=1
Case currbox
  When GBOXPO2                       : Gosub PICKE_CDE
  When GBOXSHIP1,GBOXSHIP2,GBOXSHIP4 : Gosub PICKE_SHIP
Endcase
Return

##############################################################
$PICKE_CDE
[M:PTH1]ZPALENT(NOL) = [F:POQ]ZPALENT
Affzo [M:PTH1]ZPALENT(NOL)
[M:PTH1]CPRCOE(NOL) = [F:POQ]CPRCOE
Affzo [M:PTH1]CPRCOE(NOL)
Return

##############################################################
$PICKE_SHIP
[M:PTH1]ZPALENT(NOL) = [F:SHD]ZPALENT
Affzo [M:PTH1]ZPALENT(NOL)
[M:PTH1]ZPALET(NOL) = [F:SHD]ZPALET
Affzo [M:PTH1]ZPALET(NOL)
[M:PTH1]CPRCOE(NOL) = [F:SHD]CPRCOE
[M:PTH0]ZCPRCOE = [F:SHD]CPRCOE
Affzo [M:PTH1]CPRCOE(NOL)
Affzo [M:PTH0]ZCPRCOE
Return

##############################################################
$SETBOUT
If [M:PTH0]PTHNUM <> ""
  Grizo [M:PTH0]ZCPRCOE
  Grizo [M:PTH0]ZHPRIDIV
Else
  Actzo [M:PTH0]ZCPRCOE
  Actzo [M:PTH0]ZHPRIDIV
Endif
Gosub GRIZO_CAMPOS

Return

$GRIZO_CAMPOS
Actzo [M:PTH1]ZLBPSPRI(0)
Actzo [M:PTH1]ZLPRIDIV(0)
Grizo [M:PTH1]GROPRI(0)
For I = 0 To [M:PTH1]NBLIG-1
  Grizo [M:PTH1]GROPRI(I)
  If [M:PTH0]PTHNUM <> ""
    Grizo [M:PTH1]ZLBPSPRI(I)
    Grizo [M:PTH1]ZLPRIDIV(I)
  Else
    Actzo [M:PTH1]ZLBPSPRI(I)
    Actzo [M:PTH1]ZLPRIDIV(I)
  Endif
Next

Return
##############################################################
$APRES_CRE
Local Integer LSUBC                                                                           # 06.314.025.new

Local Integer LCON

# 06.300.491.ini
  If !clalev([F:ZITM]) Then : Local File ITMMASTER  [F:ZITM] : Endif
  If !clalev([F:ZBPR]) Then : Local File BPARTNER   [F:ZBPR] : Endif
  Read [F:ZBPR]BPR0 = [M:PTH0]BPSNUM
# 06.300.491.fin

  For LCON = 0 To [M:PTH1]NBLIG-1
# JC.05102021.COST-004.INI
    Call CALC_CMREP([M:PTH1]ITMREF(LCON),[M:PTH0]PRHFCY) From ZSTKLIB
# JC.05102021.COST-004.FIN

# 06.300.491.ini
    Read [F:ZITM]ITM0 = [M:PTH1]ITMREF(LCON)
    If !fstat Then
      [F:ZITM]STAFED = [F:ZBPR]CRY
      Trbegin [F:ZITM]
      Rewrite [F:ZITM]
      If !fstat Then
        Commit
      Else
        Rollback
      Endif
    Endif
# 06.300.491.fin

# 06.314.025.ini
    If [M:PTH1]ITMKND(LCON) = 2 Then
      LSUBC = 1
    Endif
# 06.314.025.fin
  Next

# 06.314.025.ini
  If !GIMPORT and LSUBC Then
    Call ZSGARORPTH([M:PTH0]PTHNUM) From ZSGARORPTH
  Endif
# 06.314.025.fin

  Close Local File [ZITM],[ZBPR]                                              # 06.300.491.new
Return

##############################################################
$APRES_MOD
Local Integer LSUBC                                                                           # 06.314.025.new

# JC.05102021.COST-004.INI
Local Integer LCON
  For LCON = 0 To [M:PTH1]NBLIG-1
    Call CALC_CMREP([M:PTH1]ITMREF(LCON),[M:PTH0]PRHFCY) From ZSTKLIB
# 06.314.025.ini
    If [M:PTH1]ITMKND(LCON) = 2 Then
      LSUBC = 1
    Endif
# 06.314.025.fin
  Next
# JC.05102021.COST-004.FIN

# 06.314.025.ini
  If !GIMPORT and LSUBC Then
    Call ZSGARORPTH([M:PTH0]PTHNUM) From ZSGARORPTH
  Endif
# 06.314.025.fin

Return

##############################################################
$AP_ANNULE
# JC.05102021.COST-004.INI
Local Integer LCON
  For LCON = 0 To [M:PTH1]NBLIG-1
    Call CALC_CMREP([M:PTH1]ITMREF(LCON),[M:PTH0]PRHFCY) From ZSTKLIB
  Next
# JC.05102021.COST-004.FIN
Return


##############################################################
######################    ETIQUETAS    #######################
##############################################################

##############################################################
#################    ACCIONES DE CAMPO    ####################
##############################################################

##############################################################
##################    FUNCIONES PROPIAS    ###################
##############################################################
######################################################################################
## Etiqueta añadida por el supervisor (pantalla WE6ALL0) 12/11/2021 07:21:02 (ACON)
######################################################################################
Subprog AM_BPSNUM(VALEUR)
Variable Char    VALEUR()
[M:PTH0]ZCPRCOE = [F:BPS]ZCPRCOE
If [F:BPS]ZCPRCOE = 0 : [M:PTH0]ZCPRCOE=1 Endif
[M:PTH0]ZHPRIDIV = [F:BPS]ZUNPRE
Affzo [M:PTH0]ZCPRCOE
Affzo [M:PTH0]ZHPRIDIV
End

Subprog AM_ZCPRCOE(VALEUR)
Variable Decimal VALEUR
If [M:PTH1]NBLIG > 0
  Call OUINON("Repercutir el valor Arancel al detalle (SI/NO)",GBIDI2) From GESECRAN

  If GBIDI2  = 2 Then
    For I = 0 To NBLIG-1
        [M:PTH1]CPRCOE(I) = VALEUR
        Affzo [M:PTH1]CPRCOE(I)
    Next
  Endif
Endif
End


Subprog AM_ITMREF(VALEUR)
Variable Char    VALEUR()
[M:PTH1]ZITMDES2(nolign-1) = [F:ITM]ITMDES2
Affzo [M:PTH1]ZITMDES2(nolign-1)
If [M:PTH1]POHNUM(nolign-1) = ""
  [M:PTH1]CPRCOE(nolign-1)=[M:PTH0]ZCPRCOE
  [M:PTH1]ZPALENT(nolign-1) = [F:ITM]ZPALENT
  Affzo [M:PTH1]CPRCOE(nolign-1)
  Affzo [M:PTH1]ZPALENT(nolign-1)
Endif
End

Subprog AM_ZHPRIDIV(VALEUR)
Variable Char    VALEUR()
If [M:PTH1]NBLIG > 0
  Call OUINON("Repercutir el valor Unidad precio al detalle (SI/NO)",GBIDI2) From GESECRAN
  If GBIDI2  = 2 Then
    For I = 0 To NBLIG-1
        [M:PTH1]ZLPRIDIV(I) = VALEUR
        nolign=I+1
        Call AM_ZLPRIDIV(VALEUR)
        Affzo [M:PTH1]ZLPRIDIV(I)
    Next
  Endif
Endif
End

Subprog AM_ZLBPSPRI(VALEUR)
Variable Decimal VALEUR
Local Integer DIV : DIV = 1
Case [M:PTH1]ZLPRIDIV(nolign-1)
  When "UN" : DIV = 1
  When "CEN" : DIV = 100
  When "MIL": DIV = 1000
Endcase

[M:PTH1]GROPRI(nolign-1) = VALEUR/DIV
Call PAMDISCRG([M:PTH1]GROPRI(nolign-1),"PTH1",0,nolign-1,[M:PTH1]NETCUR(nolign-1))
Call PAMVATFLG("PTH1",nolign-1,"GROPRI") From TRTACHFAC1
Affzo [M:PTH1]GROPRI(nolign-1)
End

Subprog AM_ZLPRIDIV(VALEUR)
Variable Char    VALEUR()
Local Integer DIV : DIV = 1
Case VALEUR
  When "UN" : DIV = 1
  When "CEN" : DIV = 100
  When "MIL": DIV = 1000
Endcase

[M:PTH1]GROPRI(nolign-1) = [M:PTH1]ZLBPSPRI(nolign-1)/DIV
Call PAMDISCRG([M:PTH1]GROPRI(nolign-1),"PTH1",0,nolign-1,[M:PTH1]NETCUR(nolign-1))
Call PAMVATFLG("PTH1",nolign-1,"GROPRI") From TRTACHFAC1
Affzo [M:PTH1]GROPRI(nolign-1)
End


######################################################################################
#########################################################

Subprog PAMDISCRG (VALEUR,WABMSK,DISCRGNUM,NOLIG,WCUR)
Value  Decimal  VALEUR
Value  Char     WABMSK
Value  Integer  DISCRGNUM
Value  Integer  NOLIG
Value  Char     WCUR
#Dbgaff

#----- CHARGEMENT MOTIF MANUEL PAR DéFAUT -----#
If DISCRGNUM>0 & DISCRGNUM<10
   If !GIMPORT & VALEUR=evalue("[M:"+WABMSK+"]DISCRGVAL"+num$(DISCRGNUM)+"(NOLIG)") End : Endif
   Assign "[M:"+WABMSK+"]DISCRGREN"+num$(DISCRGNUM)+"(NOLIG)" With GDACMANPUR
Elsif DISCRGNUM=0
   #If !GIMPORT & VALEUR=evalue("[M:"+WABMSK+"]GROPRI(NOLIG)") End : Endif
   Assign "[M:"+WABMSK+"]PRIREN(NOLIG)" With GDACMANPUR
Endif
If evalue("dim([M:"+WABMSK+"]GROPRI(NOLIG))")<1 End : Endif
#----- CALCUL ET AFFICHAGE DU PRIX NET -----#
Local  Decimal  GROPRI, DISCRGVAL(0..8), NETPRI
Local  Shortint I
If DISCRGNUM<1 GROPRI=VALEUR
          Else GROPRI=evalue("[M:"+WABMSK+"]GROPRI(NOLIG)")
Endif
For I=1 To 9
    If evalue("DIM([M:"+WABMSK+"]DISCRGVAL"+num$(I)+"(NOLIG))")>0
       DISCRGVAL(I-1)=evalue("[M:"+WABMSK+"]DISCRGVAL"+num$(I)+"(NOLIG)")
    Endif
Next I
Case DISCRGNUM
  When 1 DISCRGVAL(0)=VALEUR
  When 2 DISCRGVAL(1)=VALEUR
  When 3 DISCRGVAL(2)=VALEUR
  When 4 DISCRGVAL(3)=VALEUR
  When 5 DISCRGVAL(4)=VALEUR
  When 6 DISCRGVAL(5)=VALEUR
  When 7 DISCRGVAL(6)=VALEUR
  When 8 DISCRGVAL(7)=VALEUR
  When 9 DISCRGVAL(8)=VALEUR
Endcase
Local Decimal QTY
If evalue("dim([M:"+WABMSK+"]QTYUOM(NOLIG))")>0
  QTY=evalue("[M:"+WABMSK+"]QTYUOM(NOLIG)")
Elsif evalue("dim([M:"+WABMSK+"]QTYPUU(NOLIG))")>0
  QTY=evalue("[M:"+WABMSK+"]QTYPUU(NOLIG)")
Else
  QTY=1
Endif
If dim(GPURNEGPRI)<0  Global Libelle GPURNEGPRI : Endif
Raz GPURNEGPRI
If evalue("dim([M:"+WABMSK+"]ITMREF)")>0
  If WABMSK="POC2" I=0 Else I=NOLIG : Endif
  If evalue("[M:"+WABMSK+"]ITMREF(I)")<>[F:ITM]ITMREF
    Read [ITM] ITM0=evalue("[M:"+WABMSK+"]ITMREF(I)")
    If fstat  Raz [F:ITM] : Endif
  Endif
  If [F:ITM]STOMGTCOD=1  GPURNEGPRI=2 : Endif
Endif
If dim(GCALNETLIN)>0  GCALNETLIN=NOLIG+1          : Endif
If dim(GCALNETABR)>0  GCALNETABR="[M:"+WABMSK+"]" : Endif
Call CALNET(2,[F:BPS]PLISTC,GROPRI,DISCRGVAL,QTY,NETPRI,WCUR) From TRTPRICE
If GMESSAGE<>"" Call MESSAGE(GMESSAGE) From GESECRAN : Raz GMESSAGE : Endif

Assign "[M:"+WABMSK+"]NETPRI(NOLIG)" With NETPRI
If !GIMPORT Affzo [M:PTH1]NETPRI(NOLIG) : Endif
#----- calcul et affichage du montant ligne -----#
If WABMSK="POI1"
  [M:POI1]LINAMT(NOLIG)=[M:POI1]NETPRI(NOLIG)*[M:POI1]QTYUOM(NOLIG)
  Call ARRDEV([M:POI1]LINAMT(NOLIG),[M:POI1]CUR(NOLIG)) From TRTDIV
  If !GIMPORT Affzo [POI1]LINAMT(NOLIG) : Endif
Elsif WABMSK="POH2"
  [M:POH2]LINAMT(NOLIG)=[M:POH2]NETPRI(NOLIG)*[M:POH2]QTYUOM(NOLIG)
  Call ARRDEV([M:POH2]LINAMT(NOLIG),GCUR) From TRTDIV
  If !GIMPORT Affzo [POH2]LINAMT(NOLIG) : Endif
Elsif WABMSK="PTH1"
  [M:PTH1]LINAMT(NOLIG)=[M:PTH1]NETPRI(NOLIG)*[M:PTH1]QTYUOM(NOLIG)
  Call ARRDEV([M:PTH1]LINAMT(NOLIG),GCUR) From TRTDIV
  If !GIMPORT Affzo [PTH1]LINAMT(NOLIG) : Endif
Elsif WABMSK="PNH1"
  [M:PNH1]LINAMT(NOLIG)=[M:PNH1]NETPRI(NOLIG)*[M:PNH1]QTYUOM(NOLIG)
  Call ARRDEV([M:PNH1]LINAMT(NOLIG),[M:PNH1]NETCUR(NOLIG)) From TRTDIV
  If !GIMPORT Affzo [PNH1]LINAMT(NOLIG) : Endif
Endif
End
