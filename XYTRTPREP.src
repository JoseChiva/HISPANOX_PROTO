#<AdxTL>@(#)0.0.0.0 $Revision$
# 06.308.862 - JC.02022022.VP diferent adreça entrega
# 06.312.723 - JC.14022022.Vale preparación padre
# 06.315.256 - JC.16022022.Aviso riesgo sobrepasado en plan de preparación
# 06.356.782 - JC.15062022.No rellena la dirección de entrega en las órdenes de preparación
# 06.317.883 - JC.06102022.CONSULTA  campo Total Crédito
# 06.411.043 - JC.10112022.Control crédito. Aviso si se va a sobrepasar el credito autorizado
# 06.414.395 - JC.01122022.Saldo contable con negativos
#############################################################################

$ACTION

#If GUSER="ADEV" Then : Infbox ACTION : Endif
Case ACTION
  When "SETBOUT"      : Gosub SETBOUT
  When "BOUTON"       : Gosub BOUTON
  When Default
Endcase

Return

##############################################################
######################    ACCIONES    ########################
##############################################################
##############################################################
$SETBOUT

  # JC.20052021.STR
  If GREP="M" | [M:PREP]NBLIG=0 | find("",[M:PREP]PRHNUM(0..[M:PREP]NBLIG-1)) | !find(1,[M:PREP]DLVFLG(0..[M:PREP]NBLIG-1))
    Call VIREBOUT(CHMEN,"s") From GOBJET
  Endif
  # JC.20052021.END

Return

##############################################################
$BOUTON
  Case BOUT
    When "s"  : Gosub CREA_SOR  # JC.20052021
    When "Q"
# 06.315.256.ini
      Gosub COMPRUEBA_RIESGO
      If !clalev([F:ZCR0])  Then : Local File ZCLIRIESGO  [F:ZCR0]  : Endif
      Local Integer LNUM
      Filter [F:ZCR0]
      Read [F:ZCR0] First
      LNUM = rowcount([F:ZCR0])
      If LNUM > 0 Then
        Local Clbfile LCAD(2)
        For [ZCR0]
          LCAD += "\"+[F:ZCR0]BPCNUM+"-"+[F:ZCR0]BPCNAM
        Next
        Local Integer OKCAN : OKCAN = 1 :# Cancel
        Call AVERTIR("Atención, hay clientes con riesgo sobrepasado"+LCAD,OKCAN) From GESECRAN
        If OKCAN = 1 Then
          GPE=1
          Return
        Else
    # 06.308.862.ini
          Gosub BOUTON From TRTPREPA
    # 06.312.723.ini
    #      If WOK = 0 Then
    #        Gosub UPD_DIRENTREGA
    #      Endif
          Gosub UPD_DIRENTREGA
    # 06.312.723.fin
          GPE=1
    # 06.308.862.fin
        Endif
# 06.356.782.ini
      Else
# 06.411.043.ini
        Local Integer LCON, CDTSTA
        Local Decimal LTOTCRE,LCREAUT
        If !clalev([F:ZSOH1]) Then : Local File SORDER [F:ZSOH1] : Endif
        For LCON=0 To [M:PREP]NBLIG-1
          If [M:PREP]COCHAGE(LCON) = 2 Then
            Read [F:ZSOH1]SOH0 = [M:PREP]ORINUM(LCON)
            If !fstat Then
              # Contrôle état crédit de la commande
              Call SDCDTSTA('',[F:ZSOH1]BPCORD,[F:ZSOH1]CHGTYP,0,[F:ZSOH1]CUR,[F:ZSOH1]ORDDAT,0,1,CDTSTA,LTOTCRE,LCREAUT) From TRTVENCDT
              If CDTSTA = 2 or CDTSTA = 3 or CDTSTA = 4 or CDTSTA = 5 Then
              Else
                If LTOTCRE + [F:ZSOH1]ORDINVATI > LCREAUT Then
                  Local Integer OKCAN : OKCAN = 1 :# Cancel
                  Call AVERTIR("Atención algún cliente va a sobrepasar el riesgo. ¿Continuar?",OKCAN) From GESECRAN
                  If OKCAN = 1 Then
                    GPE=1
                    Return
                    Break
                  Endif
                Endif
              Endif
            Endif
          Endif
        Next
        Close Local File [ZSOH1]
# 06.411.043.fin
        Gosub BOUTON From TRTPREPA
        Gosub UPD_DIRENTREGA
        GPE=1
# 06.356.782.fin
      Endif
      Filter [F:ZCR0]
      Close Local File [ZCR0]
# 06.315.256.fin

# 06.317.883.ini
      If val(func AFNC.PARAM("ZVPCONCRED","")) = 2 Then
        GZPRHSOH = 1
        Local Integer I
        For I=0 To [M:PREP]NBLIG-1
          If [M:PREP]COCHAGE(I) = 2 Then
            # actualiza el encurso
            # desasigna el encurso de los pedidos
            If !clalev([F:ZPRH]) Then : Local File STOPREH [F:ZPRH] : Endif
            If !clalev([F:ZSOH]) Then : Local File SORDER  [F:ZSOH] : Endif
            If !clalev([F:ZSOP]) Then : Local File SORDERP [F:ZSOP] : Endif
            If !clalev([F:ZSOQ]) Then : Local File SORDERQ [F:ZSOQ] : Endif
            Read [F:ZPRH]PRH0 = [M:PREP]PRHNUM(I)
            Read [F:ZSOH]SOH0 = [M:PREP]ORINUM(I)
            Read [F:ZSOP]SOP0 = [M:PREP]ORINUM(I);[M:PREP]ORILIN(I);[M:PREP]ORISEQ(I)
            Read [F:ZSOQ]SOQ0 = [M:PREP]ORINUM(I);[M:PREP]ORILIN(I);[M:PREP]ORISEQ(I)
            If !fstat Then
# 06.414.395.ini
#              Call  MAJMVC ([F:ZSOH]BPCORD,[F:ZSOH]CUR,[F:ZSOH]CHGTYP,0,[F:ZSOH]ORDDAT,[F:ZSOH]SOHNUM,[F:ZSOP]SALFCY,
#&                           [F:ZSOQ]QTY*[F:ZSOP]NETPRINOT,[F:ZSOQ]QTY*[F:ZSOP]NETPRIATI,'-','F',"",[F:ZSOP]SOHCAT) From TRTBPMVT
              Call  MAJMVC ([F:ZSOH]BPCORD,[F:ZSOH]CUR,[F:ZSOH]CHGTYP,0,[F:ZSOH]ORDDAT,[F:ZSOH]SOHNUM,[F:ZSOP]SALFCY,
&                           ar2([F:ZSOQ]QTY*[F:ZSOP]NETPRINOT),ar2([F:ZSOQ]QTY*[F:ZSOP]NETPRIATI),'-','F',"",[F:ZSOP]SOHCAT) From TRTBPMVT
# 06.414.395.fin
            # asigna el encurso al vale de preparación
# 06.414.395.ini
#              Call  MAJMVC ([F:ZPRH]BPCORD,[F:ZSOH]CUR,[F:ZSOH]CHGTYP,0,[F:ZPRH]SHIDAT,[F:ZPRH]PRHNUM,[F:ZPRH]STOFCY,
#&                           0.0,[F:ZSOQ]QTY*[F:ZSOP]NETPRIATI,'+','O',"",[F:ZSOH]SOHCAT) From TRTBPMVT
              Call  MAJMVC ([F:ZPRH]BPCORD,[F:ZSOH]CUR,[F:ZSOH]CHGTYP,0,[F:ZPRH]SHIDAT,[F:ZPRH]PRHNUM,[F:ZPRH]STOFCY,
&                           0.0,ar2([F:ZSOQ]QTY*[F:ZSOP]NETPRIATI),'+','O',"",[F:ZSOH]SOHCAT) From TRTBPMVT
# 06.414.395.fin
            Endif
          Endif
        Next
        GZPRHSOH = 1
      Endif
# 06.317.883.fin
  Endcase

Return

##############################################################
######################    ETIQUETAS    #######################
##############################################################
##############################################################
# JC.20052021
$CREA_SOR
Local Integer LCON
Local Char    LPRHNUM(50)
Local Integer YESNO : YESNO = 2   # yes

  Call OUINON("Se va a genera el documento SOR para los vales de preparación seleccionados\¿Deseas continuar?",YESNO) From GESECRAN
  If YESNO = 2 Then
    If !clalev([F:ZPRH]) Then : Local File STOPREH [F:ZPRH] : Endif
    If !clalev([F:ZPRE]) Then : Local File STOPRED [F:ZPRE] : Endif
    For LCON = 0 To [M:PREP]NBLIG - 1
      fstat = 0
      If LPRHNUM = "" or LPRHNUM <> [M:PREP]PRHNUM(LCON) Then
        LPRHNUM = [M:PREP]PRHNUM(LCON)
        If [M:PREP]COCHAGE(LCON) = 2 Then
#          Read [F:ZPRH]PRH0 = [M:PREP]PRHNUM(LCON)
          Filter [F:ZPRH] Where PRHNUM = [M:PREP]PRHNUM(LCON)
          Read [F:ZPRH] First
          If !fstat Then
            If [F:ZPRH]ZSGAENVIADO <> 2 Then
              Call ZSGASOR([M:PREP]PRHNUM(LCON),'') From ZSGASOR
              # actualiza cabecera vales de preparación
              Trbegin [F:ZPRH]
              [F:ZPRH]ZSGAENVIADO = 2
              Rewrite [F:ZPRH]
              If !fstat Then
                Commit
              Else
                Rollback
              Endif
              # actualiza líneas vales de preparación
              Read [F:ZPRE]PRE0 = [M:PREP]PRHNUM(LCON);[M:PREP]PRELIN(LCON)
              If !fstat Then
                If [F:ZPRE]ZEXPORT <> 2 Then
                  Trbegin [F:ZPRE]
                  [F:ZPRE]ZEXPORT = 2
                  Rewrite [F:ZPRE]
                  If !fstat Then
                    Commit
                  Else
                    Rollback
                  Endif
                Endif
              Endif
            Endif
          Endif
          Filter [F:ZPRH]
        Endif
      Else
#        LPRHNUM = [M:PREP]PRHNUM(LCON)
      Endif
    Next
    Close Local File [ZPRH],[ZPRE]
  Endif

  If dim(LCON)    > 0 Then : Kill LCON    : Endif
  If dim(LPRHNUM) > 0 Then : Kill LPRHNUM : Endif
Return

##############################################################
# 06.308.862
$UPD_DIRENTREGA
Local Integer LCON

  If !clalev([F:ZSOH0]) Then : Local File SORDER  [F:ZSOH0] : Endif
  If !clalev([F:ZPRE0]) Then : Local File STOPRED [F:ZPRE0] : Endif

  For LCON=0 To [M:PREP]NBLIG-1
    If [M:PREP]COCHAGE(LCON) = 2 and [M:PREP]PRHNUM(LCON) <> '' Then
      Read [F:ZSOH0]SOH0  = [M:PREP]ORINUM(LCON)
      If !fstat Then
        Trbegin [F:ZPRE0]
        Read [F:ZPRE0]PRE0 = [M:PREP]PRHNUM(LCON);[M:PREP]PRELIN(LCON)
        If !fstat Then
          [F:ZPRE0]ZBPAADD     = [F:ZSOH0]BPAADD
          [F:ZPRE0]ZBPDNAM0    = [F:ZSOH0]BPDNAM(0)
          [F:ZPRE0]ZBPDNAM1    = [F:ZSOH0]BPDNAM(1)
          [F:ZPRE0]ZBPDADDLIG0 = [F:ZSOH0]BPDADDLIG(0)
          [F:ZPRE0]ZBPDADDLIG1 = [F:ZSOH0]BPDADDLIG(1)
          [F:ZPRE0]ZBPDADDLIG2 = [F:ZSOH0]BPDADDLIG(2)
          [F:ZPRE0]ZBPDPOSCOD  = [F:ZSOH0]BPDPOSCOD
          [F:ZPRE0]ZBPDCTY     = [F:ZSOH0]BPDCTY
          [F:ZPRE0]ZBPDSAT     = [F:ZSOH0]BPDSAT
          [F:ZPRE0]ZBPDCRY     = [F:ZSOH0]BPDCRY
          [F:ZPRE0]ZBPDCRYNAM  = [F:ZSOH0]BPDCRYNAM
        Endif
        Rewrite [F:ZPRE0]
        If !fstat Then
          Commit
        Else
          Rollback
        Endif
      Endif
    Endif
  Next
  Close Local File [ZPRE0],[ZSOH0]
  If dim(LCON)>0 Then : Kill LCON :Endif

Return

##############################################################
# # 06.315.256
$COMPRUEBA_RIESGO
Local Integer LCON, CDTSTA
Local Decimal LTOTCRE,LCREAUT
  Raz GERR,GMESSAGE

  If !clalev([F:ZSOH1]) Then : Local File SORDER      [F:ZSOH1] : Endif
  If !clalev([F:ZCR1])  Then : Local File ZCLIRIESGO  [F:ZCR1]  : Endif

  Execsql From "5" Sql "Truncate table" - nomap + ".ZCLIRIESGO"

  For LCON=0 To [M:PREP]NBLIG-1
    If [M:PREP]COCHAGE(LCON) = 2 Then
      Read [F:ZSOH1]SOH0 = [M:PREP]ORINUM(LCON)
      If !fstat Then
        # Contrôle état crédit de la commande
        Call SDCDTSTA ([F:ZSOH1]SOHNUM,[F:ZSOH1]BPCORD,[F:ZSOH1]CHGTYP,0,"",[F:ZSOH1]ORDDAT,[F:ZSOH1]UNL,2,CDTSTA,LTOTCRE,LCREAUT) From TRTVENCDT
        If CDTSTA = 2 or CDTSTA = 3 or CDTSTA = 4 or CDTSTA = 5 Then
          [F:ZCR1]BPCNUM = [F:ZSOH1]BPCORD
          [F:ZCR1]BPCNAM = [F:ZSOH1]BPCNAM
          Trbegin [F:ZCR1]
          Write   [F:ZCR1]
          If !fstat Then
            Commit
          Else
            Rollback
          Endif
        Endif
      Endif
    Endif
  Next

  Close Local File [ZSOH1],[ZCR1]

Return

##############################################################
#################    ACCIONES DE CAMPO    ####################
##############################################################

##############################################################
##################    FUNCIONES PROPIAS    ###################
##############################################################
