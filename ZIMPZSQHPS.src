#<AdxTL>@(#)0.0.0.0 $Revision$
# 06.339.932 - JC.02052022.Errores cálculo cobertura artículos
# 06.343.688 - JC.26052022.Error usuari creació WEB. Se crean pedidos manualmente y en BBDD está con el usuario WEB como creación
########################################################################

$ACTION
#If GUSER="ADEV" Then : Infbox ACTION : Endif
#Local Char LUSER(50)
#  LUSER = GUSER                                                          # 06.343.688.new
#  GUSER = "WEB"                                                          # 06.343.688.new
  Case ACTION
    When "IMP_FERME"      : Gosub IMP_FERME
    When Default
  Endcase
#  GUSER = LUSER                                                          # 06.343.688.new
Return

$IMP_FERME
Local Decimal LDIFSOHWEB : LDIFSOHWEB = val(func AFNC.PARAM("ZDIFSOHWEB",""))
Local Decimal LIMPORTEWEB, LIMPORTEX3, LSTOCKDISP
Local Integer LERROR

  If !clalev([F:ZSQD]) Then : Local File SQUOTED [F:ZSQD] : Endif
  Filter [F:ZSQD] Where SQHNUM = [F:SQH]SQHNUM
  For [ZSQD]
    # comprueba si hay diferencia superior a lo indicado en el parámetro ZDIFSOHWEB entre el precio
    # web y el precio X3
    LIMPORTEWEB = [F:ZSQD]QTY * [F:ZSQD]GROPRI
    LIMPORTEX3  = [F:ZSQD]QTY * [F:ZSQD]ZPREOFERTA
    If abs(LIMPORTEWEB - LIMPORTEX3) > LDIFSOHWEB Then
       Call ECR_TRACE("Importe de la línea"-num$([F:ZSQD]SQDLIN)-"del pedido"-[F:ZSQD]SQHNUM-"superior a"-num$(LDIFSOHWEB)-[F:SQH]CUR,0) From GESECRAN
       Call ECR_TRACE("Precio web:"-num$([F:ZSQD]GROPRI)-"/ Precio X3:"-num$([F:ZSQD]ZPREOFERTA)-"/ Artículo:"-[F:ZSQD]ITMREF-"/ Importe web:"-num$(LIMPORTEWEB)-"/ Importe X3:"-num$(LIMPORTEX3),0)
& From GESECRAN
       Call ECR_TRACE("************************************************************************************************",0) From GESECRAN
    Endif
    # comprueba si no hay stock suficiente
# 06.339.932.ini
#    LSTOCKDISP = func ZAULIB01.STOCK_DISPONIBLE("",[F:ZSQD]ITMREF,[F:ZSQD]STOFCY,0,LERROR)
    LSTOCKDISP = func ZAULIB01.STOCK_DISPONIBLE_ITV([F:ZSQD]ITMREF,[F:ZSQD]STOFCY)
# 06.339.932.fin
    If LSTOCKDISP < [F:ZSQD]QTY Then
       Call ECR_TRACE("Stock de la línea"-num$([F:ZSQD]SQDLIN)-"del pedido"-[F:ZSQD]SQHNUM-"inferior al disponible (Disponible:"-num$(LSTOCKDISP)-[F:ZSQD]SAU+")",0) From GESECRAN
       Call ECR_TRACE("************************************************************************************************",0) From GESECRAN
    Endif

    # coste oferta
    [F:ZSQD]ZCOSOFER  = func ZAULIB01.GET_COSTEOFERTA([F:ZSQD]ITMREF,[F:ZSQD]SALFCY)
    # coste%
    [F:ZSQD]ZPORCOSTE = arr([F:ZSQD]ZCOSOFER*100,0.001)
    # cantidad justa
    Local Decimal F_QTY,LRES,F_COEF,LPHYSTO
    Local Integer I_QTY
    LRES    = func ZAULIB01.NO_MULTIPLO_CAJA([F:ZSQD]ITMREF,[F:ZSQD]QTY,F_COEF)
#    LPHYSTO = func ZAULIB01.GET_PHYSTO([F:ZSQD]ITMREF,[F:ZSQD]SALFCY)
    If mod([F:ZSQD]QTY,F_COEF) Then
      [F:ZSQD]ZCANTJUSTA = 2
    Endif
    # unidad precio
    [F:ZSQD]ZUNPRE = "CEN"
    # tipo precio
    [F:ZSQD]ZTIPOPRECIO = 4
    # guarda el usuario WEB como creador y actualizador en las líneas
    [F:ZSQD]CREUSR = "WEB"
    [F:ZSQD]UPDUSR = "WEB"
    Trbegin [F:ZSQD]
    Rewrite [F:ZSQD]
    If !fstat Then
      Commit
    Else
      Rollback
    Endif
  Next
  Close Local File [ZSQD]

# 06.343.688.ini
  If !clalev([F:ZSQH]) Then : Local File SQUOTE [F:ZSQH] : Endif
  Filter [F:ZSQH] Where SQHNUM = [F:SQH]SQHNUM
  Read [F:ZSQH] First
  If !fstat Then
    [F:ZSQH]CREUSR = "WEB"
    Trbegin [F:ZSQH]
    Rewrite [F:ZSQH]
    If !fstat Then
      Commit
    Else
      Rollback
    Endif
  Endif
  Filter [F:ZSQH]
  Close Local File [ZSQH]
# 06.343.688.fin

Return
