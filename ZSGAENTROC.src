#<AdxTL>@(#)0.0.0.0 $Revision$
# 06.299.138 - JC.12012022.envío del fichero a la carpeta ERR encaso de error para que no bloquee las peticiones batch
##########################################################################################################################

#If GUSER="ADEV" Then
Call ZSGAENTROC From ZSGAENTROC
#Endif

End

###########################################################################
Subprog ZSGAENTROC

  Call OUVRE_TRACE ("Imp. ROC") From LECFIC

#Declaración de variables
Local Char WSGATMP(250), SGARECIBIDOS(250), WSGATRATADOS(250), CFILE(250), LNOMFICHERO(250), WFICHEROIMP(250)
Local Char WFICINP1(250), FICH2(250)
Local Integer NFICHEROS, WNESTADO, NFICH2, I, M
Local Char FICHEROS(250)(1..1000)
Local Char WESTADOS(20)(1..8)

WESTADOS(1)='Waiting'
WESTADOS(2)='ReceptionPending'
WESTADOS(3)='Receiving'
WESTADOS(4)='PartiallyReceived'
WESTADOS(5)='Completed'
WESTADOS(6)='Expired'
WESTADOS(7)='Cancelled'
WESTADOS(8)='Closing'

Local Char FICHEROIMP(250), FICHEROEXP(250)
Local Char WSITE(250), WRORCODE(250), WSTATUS(250), WUPDATEDATE(250), WCHECKNEXTSITE(250)

Local Integer A, LFSTATFICHERO, NFICHEROS
Local Clbfile XDATA (10)

If !clalev([F:ZSHH]) : Local File SHIPMENT    [ZSHH] : Endif
If !clalev([F:ZSRH]) : Local File SRETURN     [ZSRH] : Endif

Call ECR_TRACE('ROC* - Cambio de estado',0) From GESECRAN

WFICHEROIMP='ROC01*'
Raz NFICHEROS

WSGATMP       = "ZSGA\SGA\IMPORTX3"
WSGARECIBIDOS = "ZSGA\SGA\SGA_to_ERP"
WSGATRATADOS  = "ZSGA\SGA\SGA_to_ERP\processed\ROC"
WSGAERROR     = "ZSGA\SGA\SGA_to_ERP\processed\ERR"                         # 06.299.138.new

CFILE=filpath(WSGARECIBIDOS,WFICHEROIMP,'xml')
ORDSYS = "dir "+CFILE+" /A-d /b"
Call SYSTEME2 (adxmac(-1),ORDSYS,"",NFICHEROS,FICHEROS) From ORDSYS
LFSTATFICHERO=0

If instr(1,FICHEROS(1),'xml')=0
 # Call ECR_TRACE('No existe ROC0*',1) From GESECRAN
  End
Endif
Call ECR_TRACE('Import: '+WFICHEROIMP+'.xml',0) From GESECRAN

For A=1 To NFICHEROS
  LNOMFICHERO=mid$(FICHEROS(A),1,len(FICHEROS(A))-4)
  WFICINP1=LNOMFICHERO
  LNOMFICHERO=func ZAULIB01.REPLACE(LNOMFICHERO,'.xml','')
  Call ECR_TRACE('Lectura: '+WFICINP1+'.xml',0) From GESECRAN
  Local Integer LERROR                                                      # 06.299.138.new

# 06.299.138.ini
    # control para aquellos ficheros que llegan desde el SGA con el nombre erróneo (ROC0xxxxxxxxxxx.0.xml)
    If WFICINP1 <> LNOMFICHERO Then
      Call  LANZA_WORKFLOW_ERRORES(WFICINP1,LERROR)
      Gosub MUEVE_FILE_ERR
      Break
    Endif
# 06.299.138.fin

  #Abrimos y leemos el fichero
  Openi filpath(WSGARECIBIDOS,WFICINP1,"xml") Using [ZZZ]

  Iomode adxifs  ""                 Using [ZZZ]
  Iomode adxirs  ""                 Using [ZZZ]
  Iomode adxium  50                 Using [ZZZ]

  Rdseq XDATA Using [ZZZ]

  I=1
  WDENTRO=2
  While WDENTRO=2
    [L]WSITE            =func GET_PROPXML(XDATA,"Site",I)
    [L]WRORCODE         =func GET_PROPXML(XDATA,"RorCode",I)
    [L]WSTATUS          =func GET_PROPXML(XDATA,"Status",I)
    [L]WUPDATEDATE      =func GET_PROPXML(XDATA,"UpdateDate",I)

    Gosub RESU_WESTADOS
    If WNESTADO <> 0 Then                                                 # 06.299.138.new
      Read [ZSHH]SHH0=WRORCODE
      If !fstat
        Gosub RESU_SHH
      Else
        Read [ZSRH]SRH0=WRORCODE
        If !fstat Then
          Gosub RESU_SRH
        Endif
      Endif
      M=I
      [L]WCHECKNEXTSITE   =func GET_PROPXML(XDATA,"Site",M)
      If [L]WCHECKNEXTSITE=''
        Break
      Endif
    Else                                                                  # 06.299.138.new
      Call ECR_TRACE('Error!!!! Estado inexistente',0) From GESECRAN      # 06.299.138.new
      LERROR  = 1                                                         # 06.299.138.new
      WDENTRO = 1                                                         # 06.299.138.new
    Endif                                                                 # 06.299.138.new

  Wend

  Openi Using [ZZZ]
  Call ECR_TRACE('Import: '+WFICHEROIMP+'.xml',0) From GESECRAN

  If LERROR = 1 Then                                                      # 06.299.138.new
    Gosub MUEVE_FILE_ERR                                                  # 06.299.138.new
  Else                                                                    # 06.299.138.new
    Gosub MUEVE_FILE
  Endif                                                                   # 06.299.138.new
Next

Call FERME_TRACE From LECFIC
If !GSERVEUR Then
    Call LEC_TRACE From LECFIC
Endif

End

$RESU_SHH
   [F:ZSHH]ZSTATUS=WNESTADO
   [F:ZSHH]ZUPDATE=WUPDATEDATE
   Rewrite [ZSHH]
# 06.299.138.ini
   If fstat <> 0
     LERROR = 1
   Endif
# 06.299.138.fin
   Call ECR_TRACE("SHH: "+WRORCODE+' Est: '+WESTADOS(WNESTADO),0) From GESECRAN
Return

$RESU_SRH
   [F:ZSRH]ZSTATUS=WNESTADO
   [F:ZSRH]ZUPDATE=WUPDATEDATE
   Rewrite [ZSRH]
# 06.299.138.ini
   If fstat <> 0
     LERROR = 1
   Endif
# 06.299.138.fin
   Call ECR_TRACE("SRH: "+WRORCODE+' Est: '+WESTADOS(WNESTADO),0) From GESECRAN
Return

$MUEVE_FILE
  ORDSYS = "move " + filpath(WSGARECIBIDOS,"", "","","","")+'\' + WFICINP1 + ".xml " + filpath(WSGATRATADOS,"", "","","","")
  Call SYSTEME2(adxmac(-1),ORDSYS,"",NFICH2,FICH2) From ORDSYS
Return

##############################################################
# 06.288.239.ini
$MUEVE_FILE_ERR
  ORDSYS = "move " + filpath(WSGARECIBIDOS,"", "","","","")+'\' + WFICINP1 + ".xml " + filpath(WSGAERROR,"", "","","","")
  Call SYSTEME2(adxmac(-1),ORDSYS,"",NFICH2,FICH2) From ORDSYS
Return
# 06.288.239.fin

$RESU_WESTADOS
WNESTADO=0
For N=1 To dim(WESTADOS)                                                  # 06.288.239.new
  If WESTADOS(N)=WSTATUS
    WNESTADO=N
    Break
  Endif
Next
Return

Funprog GET_PROPXML(XMLSTRING,VARNAME,I)
Value Clbfile XMLSTRING
Value Char VARNAME
Variable Integer I


Local Integer J
 # I=1
  I=instr(I,XMLSTRING,VARNAME+'>')
  If I
    I=instr(I+len(VARNAME),XMLSTRING,'>')
    If I
        J=instr(I+1,XMLSTRING,'<')
        If J
          End seg$(XMLSTRING,I+1,J-1)
        Endif
    Endif
  Endif
End ""

###############################################################
#**
#* envía correo con la traza y el fichero que ha generado el error
#*
#* @param PNOMFICH  > nombre del fichero ROC
#* @param PTIPO     > entrada/salida
#*!
Subprog LANZA_WORKFLOW_ERRORES(PNOMFICH,PTIPO)
Value Char  PNOMFICH,PTIPO
  # lanza un workflow para avisar de que la importación ha fallado
  Global Char GCORREO(250),GFICHERO(250),GTEXTO1(250),GTEXTO2(250),GTEXTO3(250),GASUNTO(250)
  GASUNTO   = "Error en la "+PTIPO+" ROC del fichero"-PNOMFICH
  GCORREO   = "sergi.cunill@hispanox.com"
#  GCORREO   = "joseluis.chiva@auren.es"
  GFICHERO  = PNOMFICH
  GTEXTO1   = "Errores producidos en el tratamiento del fichero"-PNOMFICH+".xml"
  GTEXTO2   = "Documento adjunto con la traza."
  Call WORKFLOW (1,"ZSF","",GUSER) From AWRK
  If dim(GCORREO)   > 0 Then : Kill GCORREO   : Endif
  If dim(GFICHERO)  > 0 Then : Kill GFICHERO  : Endif
  If dim(GTEXTO1)   > 0 Then : Kill GTEXTO1   : Endif
  If dim(GTEXTO2)   > 0 Then : Kill GTEXTO2   : Endif
  If dim(GTEXTO3)   > 0 Then : Kill GTEXTO3   : Endif
  If dim(GASUNTO)   > 0 Then : Kill GASUNTO   : Endif
End
