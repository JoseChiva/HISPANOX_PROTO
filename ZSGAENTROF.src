#<AdxTL>@(#)0.0.0.0 $Revision$
# 06.299.138 - JC.12012022.envío del fichero a la carpeta ERR encaso de error para que no bloquee las peticiones batch
# 06.314.024 - JC.10032022.Adapación importación ROF para subcontratación
##########################################################################################################################

#If GUSER="ADEV" Then
Call ZSGAENTROF From ZSGAENTROF
#Endif

End

##############################################################
Subprog ZSGAENTROF
#Declaración de variables
Local Char    WSGATMP(250), SGARECIBIDOS(250), WSGATRATADOS(250), CFILE(250), LNOMFICHERO(250), WFICHEROIMP(250)
Local Char    WFICINP1(250), FICH2(250)
Local Integer NFICHEROS, WNESTADO, NFICH2, I
Local Char    FICHEROS(250)(1..1000)
Local Char    WESTADOS(250)(1..8)
Local Char    FICHEROIMP(250), FICHEROEXP(250)
Local Integer A, LFSTATFICHERO, NFICHEROS
Local Clbfile XDATA (10)

  WESTADOS(1)='1'
  WESTADOS(2)='2'
  WESTADOS(3)='3'
  WESTADOS(4)='Partially received'
  WESTADOS(5)='Completed'
  WESTADOS(6)='Expired'
  WESTADOS(7)='Cancelled'
  WESTADOS(8)='Closed'

  If !clalev([F:ZFCY]) : Local File FACILITY    [ZFCY] : Endif

  If !clalev([F:ZSHH]) : Local File SHIPMENT    [ZSHH] : Endif
  If !clalev([F:ZSHD]) : Local File SHIPMENTD   [ZSHD] : Endif
  If !clalev([F:ZSRH]) : Local File SRETURN     [ZSRH] : Endif
  If !clalev([F:ZPTH]) : Local File PRECEIPT    [ZPTH] : Endif            # 06.314.024.new
  If !clalev([F:ZPTD]) : Local File PRECEIPTD   [ZPTD] : Endif            # 06.314.024.new

  WFICHEROIMP='ROF0*'
#  WFICHEROIMP='ABCROF022020092313471700D'
  Raz NFICHEROS

  WSGATMP       = "ZSGA\SGA\IMPORTX3"
  WSGARECIBIDOS = "ZSGA\SGA\SGA_to_ERP"
  WSGATRATADOS  = "ZSGA\SGA\SGA_to_ERP\processed\ROF"
  WSGAERROR     = "ZSGA\SGA\SGA_to_ERP\processed\ERR"                       # 06.299.138.new

  CFILE   = filpath(WSGARECIBIDOS,WFICHEROIMP,'xml')
  ORDSYS  = "dir "+CFILE+" /A-d /b"
  Call SYSTEME2 (adxmac(-1),ORDSYS,"",NFICHEROS,FICHEROS) From ORDSYS
  LFSTATFICHERO=0

  If instr(1,FICHEROS(1),'xml')=0
#    Call ECR_TRACE('No existe ROF0*',1) From GESECRAN
    End
  Endif

  Call OUVRE_TRACE ("Imp. ROF") From LECFIC

  For A=1 To NFICHEROS
    LNOMFICHERO=mid$(FICHEROS(A),1,len(FICHEROS(A))-4)
    WFICINP1=LNOMFICHERO
    LNOMFICHERO=func ZAULIB01.REPLACE(LNOMFICHERO,'.xml','')
    Local Integer LERROR                                                    # 06.299.138.new

# 06.299.138.ini
    # control para aquellos ficheros que llegan desde el SGA con el nombre erróneo (ROF0xxxxxxxxxxx.0.xml)
    If WFICINP1 <> LNOMFICHERO Then
      Call  LANZA_WORKFLOW_ERRORES(WFICINP1,LERROR)
      Gosub MUEVE_FILE_ERR
      Break
    Endif
# 06.299.138.fin

    #Abrimos y leemos el fichero
    Openi filpath(WSGARECIBIDOS,WFICINP1,"xml") Using [ZZZ]
    Iomode adxifs  ""                 Using [ZZZ]
    Iomode adxirs  ""                 Using [ZZZ]
    Iomode adxium  50                 Using [ZZZ]

    Rdseq XDATA Using [ZZZ]

    Call ECR_TRACE ("Fichero: "+LNOMFICHERO,0) From GESECRAN

    Local Integer WM, WNLIN, WFINLIN, WFINSTO

    I=1
    WSITE             =func GET_PROPXML(XDATA,"Site",I)
    WRECCODE          =func GET_PROPXML(XDATA,"RecCode",I)
    WRORCODE          =func GET_PROPXML(XDATA,"RorCode",I)
    WSTATUS           =func GET_PROPXML(XDATA,"Status",I)
    WRECDATCONTAINERS =func GET_PROPXML(XDATA,"RecDatContainers",I)
    WRECDATDATE       =func GET_PROPXML(XDATA,"RecDatDate",I)

    #contar lineas
    Call ECR_TRACE("Recep. RecCode: "+WRECCODE+' RorCode: '+WRORCODE+' Fich:'+LNOMFICHERO,0) From GESECRAN

    Openi Using [ZZZ]

    Gosub RESU_WESTADOS
    If WNESTADO <> 0 Then                                                 # 06.299.138.new
      WDEVOLUCION=1
      If WRORCODE<>''
        Read [ZSHH]SHH0=WRORCODE
        If fstat
          Read [ZSRH]SRH0=WRORCODE
          If !fstat
            WDEVOLUCION=2
            Gosub RESU_SRH
# 06.314.024.ini
          Else
            Read [F:ZPTH]PTH0=WRORCODE
            If !fstat
              Filter [F:ZPTD] Where PTHNUM = WRORCODE
              For [F:ZPTD]
                If [F:ZPTD]LINTYP = 3
                  Call ECR_TRACE ("***Recepción pedido subcontratación***",0) From GESECRAN
                  [F:ZPTH]ZRECCOD   = WRECCODE
                  [F:ZPTH]ZNUMCONT  = num$(WRECDATCONTAINERS)
                  [F:ZPTH]ZDATREC   = gdat$(val(mid$(WRECDATDATE,9,2)),val(mid$(WRECDATDATE,6,2)),val(mid$(WRECDATDATE,1,4)))
                  Trbegin [F:ZPTH]
                  Rewrite [F:ZPTH]
                  If !fstat Then
                    Commit
                  Else
                    Rollback
                  Endif
                  Goto END_ACTPTH
                Endif
              Next
              $END_ACTPTH
              Filter [F:ZPTD]
            Endif
# 06.314.024.fin
          Endif
        Else
          Gosub RESU_SHH
        Endif
      Endif
    Else                                                                  # 06.299.138.new
      Call ECR_TRACE('Error!!!! Estado inexistente',0) From GESECRAN      # 06.299.138.new
      LERROR  = 1                                                         # 06.299.138.new
      WDENTRO = 1                                                         # 06.299.138.new
    Endif                                                                 # 06.299.138.new

    Openi Using [ZECO]

    If LERROR = 1 Then                                                    # 06.299.138.new
      Gosub MUEVE_FILE_ERR                                                # 06.299.138.new
    Else                                                                  # 06.299.138.new
      Gosub MUEVE_FILE
    Endif                                                                 # 06.299.138.new

  Next

  Call FERME_TRACE From LECFIC
  If !GSERVEUR Then
    Call LEC_TRACE From LECFIC
  Endif
End

##############################################################
$MUEVE_FILE
  ORDSYS = "move " + filpath(WSGARECIBIDOS,"", "","","","")+'\' + WFICINP1 + ".xml " + filpath(WSGATRATADOS,"", "","","","")
  Call SYSTEME2(adxmac(-1),ORDSYS,"",NFICH2,FICH2) From ORDSYS
Return

##############################################################
# 06.288.239.ini
$MUEVE_FILE_ERR
  ORDSYS = "move " + filpath(WSGARECIBIDOS,"", "","","","")+'\' + WFICINP1 + ".xml " + filpath(WSGAERROR,"", "","","","")
  Call SYSTEME2(adxmac(-1),ORDSYS,"",NFICH2,FICH2) From ORDSYS
Return
# 06.288.239.fin

##############################################################
$RESU_WESTADOS
  WNESTADO=1
  For N=1 To dim(WESTADOS)                                                  # 06.288.239.new
    If WESTADOS(N)=WSTATUS
      WNESTADO=N
      Break
    Endif
  Next
Return

##############################################################
$RESU_SHH
   [F:ZSHH]ZSTATUS=WNESTADO
   [F:ZSHH]ZUPDATE=num$(datetime$)
   Rewrite [ZSHH]
# 06.299.138.ini
   If fstat <> 0
     LERROR = 1
   Endif
# 06.299.138.fin
   Call ECR_TRACE("SHH: "+WRORCODE+' Est: '+WESTADOS(WNESTADO),0) From GESECRAN
Return

##############################################################
$RESU_SRH
   Trbegin [F:ZSRH]
   [F:ZSRH]ZSTATUS=WNESTADO
   [F:ZSRH]ZUPDATE=num$(datetime$)
   Rewrite [ZSRH]
   If !fstat Then
    Commit
   Else
    Rollback
    LERROR = 1                                                              # 06.299.138.new
   Endif
   Call ECR_TRACE("SHH: "+WRORCODE+' Est: '+WESTADOS(WNESTADO),0) From GESECRAN
Return

##############################################################
Funprog GET_PROPXML(XMLSTRING,VARNAME,I)
Value Clbfile XMLSTRING
Value Char VARNAME
Variable Integer I

Local Integer J, OLDI
#  I=1
  I=instr(I,XMLSTRING,VARNAME+'>')
  If I
    I=instr(I+len(VARNAME),XMLSTRING,'>')
    If I
        J=instr(I+1,XMLSTRING,'<')
        If J
          CRETORNO=seg$(XMLSTRING,I+1,J-1)
          I+=len("\"+VARNAME)
          End CRETORNO
        Endif
    Endif
  Else
    I=OLDI
  Endif
End ""

###############################################################
#**
#* envía correo con la traza y el fichero que ha generado el error
#*
#* @param PNOMFICH  > nombre del fichero ROF
#* @param PTIPO     > entrada/salida
#*!
Subprog LANZA_WORKFLOW_ERRORES(PNOMFICH,PTIPO)
Value Char  PNOMFICH,PTIPO
  # lanza un workflow para avisar de que la importación ha fallado
  Global Char GCORREO(250),GFICHERO(250),GTEXTO1(250),GTEXTO2(250),GTEXTO3(250),GASUNTO(250)
  GASUNTO   = "Error en la "+PTIPO+" ROF del fichero"-PNOMFICH
  GCORREO   = "sergi.cunill@hispanox.com"
#  GCORREO   = "joseluis.chiva@auren.es"
  GFICHERO  = PNOMFICH
  GTEXTO1   = "Errores producidos en el tratamiento del fichero"-PNOMFICH+".xml"
  GTEXTO2   = "Documento adjunto con la traza."
  Call WORKFLOW (1,"ZSF","",GUSER) From AWRK
  If dim(GCORREO)   > 0 Then : Kill GCORREO   : Endif
  If dim(GFICHERO)  > 0 Then : Kill GFICHERO  : Endif
  If dim(GTEXTO1)   > 0 Then : Kill GTEXTO1   : Endif
  If dim(GTEXTO2)   > 0 Then : Kill GTEXTO2   : Endif
  If dim(GTEXTO3)   > 0 Then : Kill GTEXTO3   : Endif
  If dim(GASUNTO)   > 0 Then : Kill GASUNTO   : Endif
End
